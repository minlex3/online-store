plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.3.0'
}

group = 'com.store'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':payment-module')
    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'io.lettuce:lettuce-core'

    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    runtimeOnly 'io.r2dbc:r2dbc-h2'

    runtimeOnly 'com.h2database:h2'

    implementation 'org.liquibase:liquibase-core'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

openApiGenerate {
    generatorName.set("java")
    inputSpec.set("${project.rootDir}/payment-module/src/main/resources/api-spec.yaml")
    outputDir.set("$projectDir/build/generated")
    apiPackage.set("com.store.payment.client.api")
    modelPackage.set("com.store.payment.client.model")
    configOptions.set([
            hideGenerationTimestamp: "true",
            library                : "webclient",
            useJakartaEe           : "true",
            useTags                : "true",
            openApiNullable        : "false",
            serializableModel      : "true"
    ])
}

sourceSets.main.java.srcDir "$projectDir/build/generated/src/main/java"

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('compileJava') {
    dependsOn(tasks.openApiGenerate)
}
